if [[ $# -gt 0 && $1 != "--force" ]] ; then
    echo "usage: plugins-update [--force]" >&2
    return 2
fi

mkdir -p ~/.zsh/plugins
cd ~/.zsh/plugins

local last_update_format="+%Y-%m-%d"

local p
for p in "${ZSH_PLUGINS[@]}" ; do

    if ! plugin-is-cloned "$p" ; then
        git clone "${ZSH_PLUGIN_REPO[$p]}.git" "${ZSH_PLUGIN_DIR[$p]}"
        date "${last_update_format}" > $p.last-update
        continue
    fi

    # If there's no timestamp file, or if the timestamp has changed, or if --force is specified, do an
    # update
    if [[ ! -r ${p}.last-update \
            || $(date "${last_update_format}") != "$(< $p.last-update)" \
            || ${1} == "--force" ]] ; then

        (
            echo "Updating plugin $p" >&2
            cd "${ZSH_PLUGIN_DIR[$p]}"
            if [[ -z "$(git status --porcelain --untracked-files=no)" ]] ; then
                git pull --quiet --prune
            else
                echo "Skipping updates to "$p" because ${ZSH_PLUGIN_DIR[$p]} isn't clean." >&2
            fi
        )
        date "${last_update_format}" > $p.last-update
    fi

done

# vim: set ft=zsh:
